plugins {
	id 'java'
	id 'org.springframework.boot' version '3.0.2'
	id 'io.spring.dependency-management' version '1.1.0'
}

group = 'me.giobyte8.galleries'
version = '1.0.0'
sourceCompatibility = '17'

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-amqp'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'com.drewnoakes:metadata-extractor:2.18.0'
	implementation 'com.fasterxml.jackson.core:jackson-databind'
	implementation 'commons-codec:commons-codec:1.15'
	implementation 'mysql:mysql-connector-java:8.0.32'

	annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
}

// Introducing test suites blog: https://blog.gradle.org/introducing-test-suites
// Reference: https://docs.gradle.org/7.6.1/userguide/jvm_test_suite_plugin.html#sec:jvm_test_suite_usage
testing {
	suites {
		configureEach {
			useJUnitJupiter()
			dependencies {
				implementation 'org.springframework.boot:spring-boot-starter-test'
			}
		}

		test(JvmTestSuite)

		integrationTest(JvmTestSuite) {
			dependencies {
				implementation project()
				implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

				// Should it be removed in favor of MySQL test instance?
				// implementation 'com.h2database:h2'
			}

			targets {
				all {
					testTask.configure {
						shouldRunAfter(test)
					}
				}
			}
		}
	}
}

//tasks.named('check') {
//	dependsOn(testing.suites.integrationTest)
//}
